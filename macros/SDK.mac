.include "SDK_utils.mac"

;======================================================================
;			String functions
;======================================================================

;----------------------------------------------------------------------
;
; usage:
;	strcpy dest, src
;
; Call _strcpy function
;
; note:
;	dest: may be nothing, AY, (ptr), address
;	src : may be nothing, AY, (ptr), address
;----------------------------------------------------------------------
.macro strcpy dest, src
	SDK_call_function _strcpy, {dest}, {src}
.endmacro


;----------------------------------------------------------------------
;
; usage:
;	strcat dest, src
;
; Call _strcat function
;
; note:
;	dest: may be nothing, AY, (ptr), address
;	src : may be nothing, AY, (ptr), address
;----------------------------------------------------------------------
.macro strcat dest, src
	SDK_call_function _strcat, {src}, {dest}
.endmacro

;----------------------------------------------------------------------
;
; usage:
;	strlen str
;	strlen (ptr)
;
; Call _strlen function
;
;----------------------------------------------------------------------
.macro strlen str
	; TODO: Reject immediate mode
	SDK_get_AY str


	sta RES
	sty RES+1
	jsr _strlen
.endmacro

;======================================================================
;				Print
;======================================================================

;----------------------------------------------------------------------
;
; usage:
;	print #byte [,TELEMON|NOSAVE]
;	print (pointer) [,TELEMON|NOSAVE]
;	print address [,TELEMON|NOSAVE]
;
; Option:
;	- TELEMON: when used within TELEMON bank
;	- NOSAVE : does not preserve A,X,Y registers
;
; Call XWSTR0 function
;
;----------------------------------------------------------------------
.macro print str, option

	.if (.not .blank({option})) .and (.not .xmatch({option}, NOSAVE)) .and (.not .xmatch({option}, TELEMON) )
		.error .sprintf("Unknown option: '%s' (not in [NOSAVE,TELEMON])", .string(option))
	.endif

	;.if (.not .blank({option})) .and .xmatch({option}, NOSAVE)
	;	.out "Don't save regs values"
	;.endif

	.if .blank({option})
		pha
		txa
		pha
		tya
		pha
	.endif

	.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
		pha
		txa
		pha
		tya
		pha

		lda RES
		pha
		lda RES+1
		pha
	.endif


	SDK_imm_or_ind_or_abs str
	;SDK::inst_mode .set $01

	.if SDK::inst_mode = 1
		;.out "Mode: immediat"
		.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
			jsr XWR0_ROUTINE
		.else
			.byte $00, XWR0
		.endif

	.elseif SDK::inst_mode = 2
		;.out "Mode: indirect"
		.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
			jsr XWSTR0_ROUTINE
		.else
			.byte $00, XWSTR0
		.endif

	.elseif SDK::inst_mode = 3
		;.out "Mode:absolu"
		.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
			jsr XWSTR0_ROUTINE
		.else
			.byte $00, XWSTR0
		.endif

	.else
		.error .sprintf("Incorrect mode: %s", .string(str))
	.endif


	.if .blank({option})
		pla
		tay
		pla
		tax
		pla
	.endif

	.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
		pla
		sta RES+1
		pla
		sta RES

		pla
		tay
		pla
		tax
		pla
	.endif

.endmacro

;======================================================================
;			Memory functions
;======================================================================

;----------------------------------------------------------------------
;
; usage:
;	malloc size [,ptr] [,oom_msg_ptr] [,fail_value]
;
;	malloc #$0100
;	malloc (ptr)
;	malloc value
;
; Note:
;	- if parameter 'ptr' is present, store resulting AY in ptr &ptr+1
;	- if parameter 'oom_msg_ptr' is present, emit string pointed by
;		'oom_msg_ptr' and return if AY is null (ie malloc error)
;
; Call XMALLOC function
;
;----------------------------------------------------------------------
.macro malloc size, ptr, oom_msg_ptr, fail_value

	;SDK_imm_or_ind_or_abs size
	SDK_get_AY size

	;----------------------------------------------------------
	;		   Le mode immediat n'accepte qu'une valeur
	;		   sur 1 octet.
	;----------------------------------------------------------
	.if SDK::inst_mode = $01
		ldy #$00
	.endif

	.if SDK::inst_mode <> $ff
		.byte $00, XMALLOC
	.endif

	.if (.not .blank({oom_msg_ptr}))
		check_regs AY, 0, oom_msg_ptr, fail_value

	.elseif (.not .blank({fail_value}))
		check_regs AY, 0, oom_msg_ptr, fail_value
	.endif

	.if (.not .blank({ptr}))
		sta ptr
		sty ptr+1
	.endif

.endmacro

;----------------------------------------------------------------------
;
; usage:
;	mfree (ptr)
;
; Call XFREE function
;----------------------------------------------------------------------
.macro mfree addr
	.if (.match(.left(1, {addr}), {(}) )
		; Indirect

		.if (.match(.right(1,{addr}), {)}))
			; .out"Indirect mode"

			lda .mid (1,.tcount ({addr})-2, {addr})
			ldy 1+(.mid (1,.tcount ({addr})-2, {addr}))

		.else
			.error "error: ')' missing"
		.endif

	.else
		; assume absolute
		; .out "Aboslute mode"
		.error "error: indirect mode only"
	.endif

	.byte $00, XFREE
.endmacro

;======================================================================
;				File
;======================================================================

;----------------------------------------------------------------------
;
; usage:
;	fopen file, mode [,TELEMON] [,ptr] [,oom_msg_ptr] [,fail_value]
;
; note:
;	- file may be: (ptr), address
;	- if parameter 'ptr' is present, store resulting AY in ptr & ptr+1
;	- if parameter 'oom_msg_ptr' is present, emit string pointed by
;		'oom_msg_ptr' and return if AY is null (ie malloc error)
;
; Call XOPEN function
;----------------------------------------------------------------------
.macro fopen file, mode, option, ptr, oom_msg_ptr, fail_value

	.if (.not .blank({option})) .and (.not .xmatch({option}, TELEMON) )
		.error .sprintf("Unknown option: '%s' (not in [TELEMON])", .string(option))
	.endif

	; TODO: Reject immediate mode
	SDK_get_AX file

	; TODO: Accept absolute & indirect mode
	ldy #(mode)

	.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
		jsr XOPEN_ROUTINE
	.else
		.byte $00, XOPEN
	.endif


	.if (.not .blank({oom_msg_ptr}))
		check_regs AX, $ff, oom_msg_ptr, fail_value

	.elseif (.not .blank({fail_value}))
		check_regs AX, $ff, oom_msg_ptr, fail_value
	.endif

	.if (.not .blank({ptr}))
		sta ptr
		sty ptr+1
	.endif

.endmacro

;----------------------------------------------------------------------
;
; usage:
;	fread ptr, size, count, fp
;
; note:
;	ptr may be : (ptr), address
;	size may be: (ptr), address
;
; Call XFREAD function
;----------------------------------------------------------------------
.macro fread ptr, size, count, fp

	; TODO:Reject immediate mode for ptr
	SDK_get_AY ptr
	sta PTR_READ_DEST
	sty PTR_READ_DEST+1

	SDK_get_AY size

	.byte $00, XFREAD
.endmacro

;----------------------------------------------------------------------
;
; usage:
;	fclose (fp) [,TELEMON]
;
; Call XCLOSE function
;----------------------------------------------------------------------
.macro fclose fp, option

	.if (.not .blank({option})) .and (.not .xmatch({option}, TELEMON) )
		.error .sprintf("Unknown option: '%s' (not in [TELEMON])", .string(option))
	.endif

	.if (.match(.left(1, {fp}), {(}) )
		; Indirect

		.if (.match(.right(1,{fp}), {)}))
			; .out"Indirect mode"

			lda .mid (1,.tcount ({fp})-2, {fp})
			ldy 1+(.mid (1,.tcount ({fp})-2, {fp}))

		.else
			.error "error: ')' missing"
		.endif

	.else
		; assume absolute
		; .out "Aboslute mode"
		.error "error: indirect mode only"
	.endif

	.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
		;lda fp
		;ldy fp+1
		jsr XFREE_ROUTINE
		jsr XCLOSE_ROUTINE

	.else
		;lda fp
		;ldy fp+1
		.byte $00, XFREE
		.byte $00, XCLOSE

	.endif

.endmacro

;----------------------------------------------------------------------
;
; usage:
;	mkdir ptr
;
; note:
;	str may be: (ptr), address
;
; Call XMKDIR function
;----------------------------------------------------------------------
.macro mkdir ptr, option

	.if (.not .blank({option})) .and (.not .xmatch({option}, TELEMON) )
		.error .sprintf("Unknown option: '%s' (not in [TELEMON])", .string(option))
	.endif

	; TODO:Reject immediate mode for ptr
	SDK_get_AX ptr

	.if (.not .blank({option})) .and .xmatch({option}, TELEMON)
		jsr XMKDIR_ROUTINE

	.else
		.byte $00, XMKDIR

	.endif

.endmacro

;======================================================================
;			ARGV/ARGC
;======================================================================

;----------------------------------------------------------------------
; init_argv
;
; usage:
;	init_argv argv, cmdline
;
; Note:
;	argv must the address of a buffer
;	argv may be   : address, (ptr), #value
;	cmdline may be: address, (ptr), #value
;
; Initialize _argv array & _argc, must be called before get_argv
;----------------------------------------------------------------------
.macro init_argv argv, bufedt
	SDK_call_function _init_argv, {bufedt}, {argv}

	; A intégrer dans _init_argv
	tax
	sta _argc

	lda RES
	ldy RES+1
	sta _argv
	sty _argv+1
.endmacro

;----------------------------------------------------------------------
; get_argv
;
; usage:
;	ldx index
;	get_argv
;
; Note:
;	Use _argv
;
; Get the argument N° X from _argv
;----------------------------------------------------------------------
.macro get_argv
	; X doit contenir l'index
	; _argv et _argc doivent être initialisés
	jsr _get_argv
.endmacro


;======================================================================
;				Misc
;======================================================================
;----------------------------------------------------------------------
;
; usage:
;	cursor ON|OFF
;
; Call XCSSCR/XCOSCR functions
;----------------------------------------------------------------------
.macro cursor mode
	.if (.xmatch(.string(mode), .string(ON)) .or .xmatch(.string(mode), .string(on)))
		ldx #$00
		.byte $00, XCSSCR

	.elseif (.xmatch(.string(mode), .string(OFF)) .or .xmatch(.string(mode), .string(off)))
		ldx #$00
		.byte $00, XCOSCR

	.else
		.error .sprintf("Unknown parameter value: %s (must be on or off)", .string(mode))
	.endif
.endmacro

;----------------------------------------------------------------------
;
; usage:
;----------------------------------------------------------------------
.macro MODULE start, end, exec
	.import __MAIN_START__
	.import __MAIN_LAST__

	.export __ORIXHDR__:abs = 1
	.segment "ORIXHDR"
		.byte $01,$00		; non-C64 marker like o65 format
		.byte "o", "r", "i"	; "ori" MAGIC number :$6f, $36, $35 like o65 format
		.byte $01		; version of this header
	cpu_mode:
		.byte $00		; CPU see below for description
	language_type:
		.byte $00		; reserved in the future, it will define if it's a Hyperbasic file, teleass file, forth file
		.byte $00		; reserved
		.byte $00		; reserved
		.byte $00
		.byte $00		; reserved
		.byte $00		; reserved
	type_of_file:
		.byte $00
;		.word start		; loading adress
;		.word end		; end of loading adress
;		.word exec		; starting adress
		.word __MAIN_START__
		.word __MAIN_LAST__
;		.word __MAIN_START__	; starting adress
		.word exec
.endmacro


;======================================================================
;			Pointer functions
;======================================================================
;----------------------------------------------------------------------
; check_regs
;
; usage:
;	check_regs regs [,err_value] [,oom_msg_ptr] [,fail_value]
;
; Note:
;	regs is one of: AX, AY, XY, XA, YA, YX
;
; Check regs pair, display oom_msg_ptr and return if regs pairs is err_value
;----------------------------------------------------------------------
.macro check_regs regs, err_value, oom_msg_ptr, fail_value
	.local exit
	.local error

	.if .not .blank(err_value)
		error .set err_value
	.else
		error .set 0
	.endif

	.if ( .xmatch({regs}, AY) .or .xmatch({regs}, YA))
		cpy #error
		bne exit
		cmp #error
		bne exit

	.elseif ( .xmatch({regs}, AX) .or .xmatch({regs}, XA))
		cpx #error
		bne exit
		cmp #error
		bne exit

	.elseif ( .xmatch({regs}, XY) .or .xmatch({regs}, YX))
		cpx #error
		bne exit
		cpy #error
		bne exit

	.else
		.error .sprintf("Unknown registers: %s (must be 'AX', 'XA', 'AY', 'YA', 'XY', 'YX')", .string(regs))
	.endif


	.if (.not .blank({oom_msg_ptr}))
		print oom_msg_ptr, NOSAVE

		sec

		.if (.not .blank({fail_value}))
			SDK_get_AY fail_value

			; Si mode immediat, on force Y à 0
			.if SDK::inst_mode = 1
				ldy #$00
			.endif
			;ldy #>(fail_value)
			;lda #<(fail_value)
		.else
			ldy #$ff
			tya
		.endif

	.elseif (.not .blank({fail_value}))
		SDK_get_AY fail_value

		; Si mode immediat, on force Y à 0
		.if SDK::inst_mode = 1
			ldy #$00
		.endif
		;ldy #>(fail_value)
		;lda #<(fail_value)
	.endif

		rts
	exit:
.endmacro

